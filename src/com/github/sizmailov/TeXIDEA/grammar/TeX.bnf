{
  parserClass="com.github.sizmailov.TeXIDEA.parser.TeXParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="TeX"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.sizmailov.TeXIDEA.psi"
  psiImplPackage="com.github.sizmailov.TeXIDEA.psi.impl"

  elementTypeHolderClass="com.github.sizmailov.TeXIDEA.psi.TeXTypes"
  elementTypeClass="com.github.sizmailov.TeXIDEA.psi.TeXElementType"
  tokenTypeClass="com.github.sizmailov.TeXIDEA.psi.TeXTokenType"

  tokens=[
        ENVIRONMENT_BEGIN_TOKEN = "\begin"
        ENVIRONMENT_END_TOKEN = "\end"
        DOLLARS = "$+"
        INLINE_MATH_DOLLAR = "$"
        INLINE_MATH_BEGIN_PARENTHESES = "\("
        INLINE_MATH_END_PARENTHESES = "\)"
        DISPLAY_MATH_DOLLAR = "$$"
        DISPLAY_MATH_BEGIN_PARENTHESES = "\["
        DISPLAY_MATH_END_PARENTHESES = "\]"
        NEWCOMMAND_TOKEN = 'regexp:(\\(re)newcommand\*?)'
        COMMAND = 'regexp:(\\([a-zA-Z@]+|.|\n|\r))'
        WORD = 'regexp:[^\s\\{}%\[\]$\(\)]+'
        NEW_LINE = 'regexp:[\r\n]*'
        SPACE = 'regexp:\s+'
        BRACE_LEFT = "{"
        BRACE_RIGHT = "}"
        BRACKET_LEFT = "["
        BRACKET_RIGHT = "]"
        PARENTHESES_LEFT = "("
        PARENTHESES_RIGHT = ")"
        COMMENT_CONTEXT_TOKEN = 'regexp:%[^\r\n]*'
        ENVIRONMENT_IDENTIFIER = 'regexp:([^\${}]+)'
]
}


latexFile ::= context*

private context ::= comment_context | environment_context | math_context | text_context | generic_context | newcommand_context

comment_context ::= COMMENT_CONTEXT_TOKEN
newcommand_context ::= NEWCOMMAND_TOKEN newcommand_name ( "[" newcommand_nargs "]" ("[" newcommand_optarg "]")? )? "{" newcommand_body "}"
newcommand_name ::=  "{" COMMAND "}" | COMMAND
newcommand_nargs ::= WORD
newcommand_optarg ::= (group | WORD | COMMAND | "(" | ")" | "["  | DOLLARS)*
newcommand_body ::= generic_context?

generic_context ::= generic_content
generic_content ::= generic_element+
private generic_element ::= group | WORD | COMMAND | brackets | DOLLARS

text_context ::= text_content
text_content ::= text_element+

private text_element ::= group | WORD | COMMAND | brackets

private brackets ::= "(" | ")" | "[" | "]"

group ::= "{" context* "}"

math_context ::= inline_math_context | display_math_context


display_math_context ::= "$$" math_content "$$" |
                         "\[" math_content "\]"

inline_math_context ::=  "$" math_content "$" |
                         "\(" math_content "\)"


math_content ::= math_element*
private math_element ::= group | COMMAND | WORD | brackets | environment_context


environment_begin ::= "\begin" "{" ENVIRONMENT_IDENTIFIER "}"
environment_end ::= "\end" "{" ENVIRONMENT_IDENTIFIER "}"

environment_context ::= environment_begin environment_content environment_end
environment_content ::= context*



